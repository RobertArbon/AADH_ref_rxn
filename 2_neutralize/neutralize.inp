* NEUTRALIZE system 
* First this does some renaming of segments etc. 
* Then saves the results to the local directory with the same file names. 
* These new files get used in the MC loop
*

prnlev 5
set molname 1-as-solvated

stream ../common/toppar_water_ions_fixed.str
read rtf card name ../common/aadh_topologies.rtf append flex
read rtf card name ../common/top_all36_cgenff.rtf append flex
read para card name ../common/par_all36_prot_mod.prm append flex
read para card name ../common/aadh_parameters.prm append flex
read para card name ../common/par_all36_cgenff.prm append flex

read psf card name ../common/@molname.psf
read coord card name ../common/@molname.cor

SET XDIM 43
SET YDIM 43
SET ZDIM 43
calc cutf 16


! Determine what type of ions being used
set nneg CLA                     ! set segid of neg. ion
set negion CL                    ! set atom type of neg. ion
set npos POT                     ! set segid of pos. ion
set posion POT                   ! set atom type of pos. ion

! definitions

define prot sele .not. segid bwat  end

! Setup coor volume scalar arrays
scalar radi store 1
scalar wmain set 0.5
scalar wmain store 2

! determine the net charge on the system
scalar charge stat sele all end
set chrg = ?stot

! Compute volume of entire box !volspace sets up a grid to compute the free volume
calc volspace ?NATO * 160
coor volume space @volspace select all end
show ?VOLUME
show ?FREEVOL
calc tvol ?VOLUME + ?FREEVOL

! Compute volume of just the protein
define tmp select prot end
show ?NSEL
calc volspace ?NSEL * 160
coor volume space @volspace select prot end
show ?VOLUME
show ?FREEVOL
calc protvol ?VOLUME + ?FREEVOL

! Compute just the volume of the water
calc wvol @tvol - @protvol

! Compute how many K+/Cl- are needed for 0.15M concentration
set concentration 0.15
calc volconv 1 * 0.000000000000000000000001
calc wvoll ( ( @wvol * @volconv ) / 1000 )
calc avog 1 * 602213670000000000000000

FORMat (I5)
if @chrg gt 0 then
   calc totions ( @wvoll * @avog * @concentration )
   calc nions ( @wvoll * @avog * @concentration ) - @chrg
else
   calc totions ( @wvoll * @avog * @concentration )
   calc nions ( @wvoll * @avog * @concentration ) + @chrg
endif

! Make sure that an even number of charges are added
calc tmpb int(@nions / 2) * 2
calc tmpc @nions - @tmpb
if @tmpc eq 1 then
   if @chrg .gt. 0 then
      calc nions @nions + 1
   else
      calc nions @nions - 1
   endif
endif

! Setup number of charges
if @chrg gt 0 then
   calc ineg ( @nions / 2 ) + @chrg        ! no. negative ions (i.e. chlorides)
   calc ipos ( @nions / 2 )                ! no. positive ions (i.e. potassiums)
else
   calc ineg ( @nions / 2 )                ! no. negative ions (i.e. chlorides)
   calc ipos ( @nions / 2 ) - @chrg        ! no. positive ions (i.e. potassiums)
endif

format                                    ! reset formating
set mnd 5.5                               ! minimum distance to solute, other ions
set sol segid AL* .or. segid BT* .or. segid XWAT          ! atoms selection for solvent ???
set emin 1E20                             ! initial min energy value
set ncfg 1                                ! initialize loop counter
set last 3                                ! no. of passes thru the loop
random uniform iseed 314159               ! change iseed to sample diff states

! BEGINNING OF MAIN MONTE-CARLO LOOP
label placion
time now

open unit 10 write card name @molname.log
outu 10
! LOG FILE FOR PASS @ncfg

! re-read the initial PSF and CRD at the beginning of the main loop
read psf card name ../common/@molname.psf
read coor card name ../common/@molname.cor

! RANDOM WATER REPLACEMENT
set IONTYP = POT
stream addions.str

! RENUMBER THE WATER MOLECULES
join bwat renum

! SETUP CRYSTAL (DEFINE, BUILD), IMAGE CENTERING W. MODIFIED PSF
! WE USE THE SAME PARAMS AS IN SOLVATION
CRYSTAL DEFI cubi @xdim @ydim @zdim 90.0 90.0 90.0
CRYSTAL BUILD CUTOFF @CUTF NOPER 0
IMAGE BYRES SELE all END

!NON BONDED SPECIFICATION
set ftx 48
set fty 48
set ftz 48
calc kp 40/14

NBONDED inbfrq -1 imgfrq -1 nbxmod 5 -                                              ! C36 DEFAULTS
        atom cdiel fshift -                                                         ! use atom based electrostatics, C36 DEFAULTS
        vatom vdistance vfswitch -                                                  ! C36 DEFAULTS
        elec ewald pmew fftx @ftx ffty @fty fftz @ftz kappa @kp spline order 6 -    ! PME SUMMATION
        cutnb 14.0 ctofnb 12.0 ctonnb 10.0 -                                        ! C36 DEFAULTS
        cutim 14.0 -                                                                ! must be same as cutnb
        eps 1.0 e14fac 1.0 wmin 1.5                                                 ! C36 DEFAULTS


! BRIEF MIN OF IONS INSERTED INTO SOLVATED MODEL
mini sd nstep 10 nprint 1

! add shake for ABNR step
shake bonh param sele all end

mini abnr nstep 25 nprint 5

! KEY LOGICAL TEST; CONFIGS WITH HIGHER ENERGY REJECTED
if emin .lt. ?ENER goto test

! WRITE THE LATEST MINIMUM ENERGY RESULT; CONFIG ACCEPTED
write psf card name @molname-neut.psf
* @molname neutralized with potassium chloride 
*

! DO AN UPDATE AND WRITE THE COOR FILE
update

ioformat extended

write coor card name @molname-neut.cor
* Neutralized structure.
*

! UPDATE MINIMUM ENERGY
set emin ?ENER

! TEST FOR EXIT, AND SETUP FOR NEXT PASS; REVERT TO STDOUT, CLOSE LOG
label test
crystal free
shake off
incr ncfg by 1
time diff
outu 6
close unit 10
if ncfg le @LAST goto placion

stop

